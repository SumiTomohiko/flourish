
from random import random
from sdl import SDL_BUTTON, SDL_BUTTON_LEFT
import yogame

from flourish.char.stone import Stone
from flourish.core import CELL_SIZE, CHAR_POS_OFFSET_X, CHAR_POS_OFFSET_Y, FPS, MAIN_VIEW_PC_X, MAIN_VIEW_PC_Y, MAIN_VIEW_X, MAIN_VIEW_Y, MAP_SIZE, Point, Rect
from flourish.geometry import Goal

class Rect2Act
  def init(rect, act)
    self.pred? = rect.include?
    self.act = act
  end
end

class Controller > yogame.Controller
  def init(models)
    super()

    self.map = models['map]
    self.pc = models['pc]
    self.npc = models['npc]
    self.npc.each(&self.place_npc)

    self.coroutine = nil
    self.coroutines = self.make_coroutines()

    x = MAIN_VIEW_X + MAIN_VIEW_PC_X
    y = MAIN_VIEW_Y + MAIN_VIEW_PC_Y
    pc_cell_left_x = x - CELL_SIZE // 2
    move_forward_area = Rect.new(pc_cell_left_x, y - CELL_SIZE - CELL_SIZE // 2)

    turn_area_y = y - CELL_SIZE
    pc_cell_right_x = x + CELL_SIZE // 2
    turn_left_area = Rect.new(pc_cell_left_x - CELL_SIZE, turn_area_y)
    turn_right_area = Rect.new(pc_cell_right_x, turn_area_y)

    backward_area_height = CELL_SIZE // 2
    move_backward_area = Rect.new(
      pc_cell_left_x, y + CELL_SIZE // 2,
      backward_area_height, backward_area_height)

    move_side_y = turn_area_y + CELL_SIZE
    move_left_area = Rect.new(pc_cell_left_x - CELL_SIZE, move_side_y)
    move_right_area = Rect.new(pc_cell_right_x, move_side_y)

    set_move_proc = self.set_move_proc
    on_forward_area_pressed = set_move_proc.partial(self.move_forward)
    on_backward_area_pressed = set_move_proc.partial(self.move_backward)
    on_left_area_pressed = set_move_proc.partial(self.move_left)
    on_right_area_pressed = set_move_proc.partial(self.move_right)
    self.rect2act = [
      Rect2Act.new(move_forward_area, on_forward_area_pressed),
      Rect2Act.new(turn_left_area, self.turn_left),
      Rect2Act.new(turn_right_area, self.turn_right),
      Rect2Act.new(move_backward_area, on_backward_area_pressed),
      Rect2Act.new(move_left_area, on_left_area_pressed),
      Rect2Act.new(move_right_area, on_right_area_pressed)]
    self.invalidate_move_proc()
  end

  def invalidate_move_proc()
    self.set_move_proc(nop)
  end

  def set_move_proc(f)
    self.move_proc = f
  end

  def movable?(char, direction)
    pos = self.get_next_pos(char.x, char.y, direction)
    return !self.wall?(pos.x, pos.y) && !self.conflict?(char, pos)
  end

  def get_index(x, y)
    return Point.new(x // CELL_SIZE, y // CELL_SIZE)
  end

  def conflict?(char, pos)
    index = self.get_index(pos.x, pos.y)
    ([self.pc] + self.npc).each() do [c]
      if (c == char) || (c.x == nil) || (c.y == nil)
        next
      end
      if (c.x - CHAR_POS_OFFSET_X) % CELL_SIZE == 0
        if c.x // CELL_SIZE != index.x
          next
        end
        top_y = c.y - CHAR_POS_OFFSET_Y
        if top_y // CELL_SIZE == index.y
          return true
        end
        if (top_y + CELL_SIZE - 1) // CELL_SIZE == index.y
          return true
        end
      elif (c.y - CHAR_POS_OFFSET_Y) % CELL_SIZE == 0
        if c.y // CELL_SIZE != index.y
          next
        end
        left_x = c.x - CHAR_POS_OFFSET_X
        if left_x // CELL_SIZE == index.x
          return true
        end
        if (left_x + CELL_SIZE - 1) // CELL_SIZE == index.x
          return true
        end
      end
    end
    return false
  end

  def move_char(char, direction, len)
    direction = char.direction
    if direction == 'north
      char.y -= len
    elif direction == 'east
      char.x += len
    elif direction == 'south
      char.y += len
    else
      char.x -= len
    end
  end

  def get_next_pos(x, y, direction)
    pos = Point.new(x, y)
    if direction == 'north
      pos.y -= CELL_SIZE
    elif direction == 'east
      pos.x += CELL_SIZE
    elif direction == 'west
      pos.x -= CELL_SIZE
    else
      pos.y += CELL_SIZE
    end
    return pos
  end

  def place_npc(char)
    if (char.x != nil) && (char.y != nil)
      return
    end

    rand = random.partial(MAP_SIZE - 2, 1)
    x = rand() * CELL_SIZE + CHAR_POS_OFFSET_X
    y = rand() * CELL_SIZE + CHAR_POS_OFFSET_Y
    pos = Point.new(x, y)
    if !self.geometry?(x, y, nil) || self.conflict?(char, pos)
      return self.place_npc(char)
    end

    char.x = x
    char.y = y
  end

  def attack_pc(strength)
    self.pc.life -= strength
    if 0 < self.pc.life
      return
    end
    self.pc.die()
    self.exit('dead)
  end

  def make_coroutine(char)
    return Coroutine.new() do
      loop() do
        char.act(self)
      end
    end
  end

  def make_coroutines()
    return self.npc.map(&self.make_coroutine)
  end

  def remove_dead_coroutine()
    if (self.coroutine != nil) && self.coroutine.dead?
      self.coroutine = nil
    end
  end

  def start_coroutine()
    self.move_proc()
    if self.coroutine == nil
      return
    end
    self.coroutine.resume()
  end

  def animate()
    self.coroutines.each() do [coro]
      coro.resume()
    end
    self.remove_dead_coroutine()
    if self.coroutine == nil
      self.start_coroutine()
      return
    end
    self.coroutine.resume()
  end

  def move_right()
    self.coroutine = Coroutine.new(&self.pc.move_right.partial(self))
  end

  def move_left()
    self.coroutine = Coroutine.new(&self.pc.move_left.partial(self))
  end

  def move_backward()
    self.coroutine = Coroutine.new(&self.pc.move_backward.partial(self))
  end

  def geometry?(x, y, expected)
    return self.map[y // CELL_SIZE][x // CELL_SIZE] == expected
  end

  def wall?(x, y)
    return self.geometry?(x, y, 'wall)
  end

  def path?(x, y)
    return self.geometry?(x, y, nil)
  end

  def goal?()
    pc = self.pc
    return self.map[pc.y // CELL_SIZE][pc.x // CELL_SIZE].kind_of?(Goal)
  end

  def check_goal()
    if !self.goal?()
      return
    end
    self.exit('goal)
  end

  def find_npc(char)
    # TODO: Implement Array#find.
    self.npc.each_with_index() do [i, c]
      if char == c
        return i
      end
    end
    # TODO: Change to better exception class.
    raise Exception.new("NPC not found")
  end

  def kill_npc(char)
    char.die()
    char.regenerate()
    self.place_npc(char)
    self.coroutines[self.find_npc(char)] = self.make_coroutine(char)
  end

  def attack_npc(char)
    char.life -= self.pc.strength
    if 0 < char.life
      return
    end
    self.kill_npc(char)
  end

  def move_forward()
    pc = self.pc
    self.npc.each() do [char]
      if !char.kind_of?(Stone) && pc.attackable?(char)
        self.coroutine = Coroutine.new(&pc.attack.partial(self, char))
        return
      end
    end

    self.coroutine = Coroutine.new(&pc.move_forward.partial(self))
  end

  def turn(f)
    if self.coroutine != nil
      return
    end
    self.coroutine = Coroutine.new(&f)
  end

  def turn_right()
    return self.turn(self.pc.turn_right)
  end

  def turn_left()
    return self.turn(self.pc.turn_left)
  end

  def on_left_button_pressed(x, y)
    self.rect2act.each() do [obj]
      if obj.pred?(x, y)
        return obj.act()
      end
    end
  end

  def on_mousemotion(state, x, y, xrel, yrel)
    if state & SDL_BUTTON(SDL_BUTTON_LEFT) == 0
      return
    end
    return self.on_left_button_pressed(x, y)
  end

  def on_left_button_released(x, y)
    self.invalidate_move_proc()
  end
end

# vim: tabstop=2 shiftwidth=2 expandtab softtabstop=2
