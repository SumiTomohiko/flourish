
import sdl
import sdl.mixer
import sdl.ttf
import yogame

SCREEN_WIDTH = 640
SCREEN_HEIGHT = 480

MAP_SIZE = 32

TURN_RIGHT_TABLE = {
  'north: 'east, 'east: 'south, 'south: 'west, 'west: 'north }
TURN_LEFT_TABLE = {
  'north: 'west, 'west: 'south, 'south: 'east, 'east: 'north }

def determine_data_dir()
  data_dir = __FILE__.dirname / ".." / ".." / "data"
  landmark = data_dir / "mouse_area.png"
  try
    File.open(landmark, &nop)
  except SystemError
  else
    return data_dir
  end
  return __FILE__.dirname / ".." / ".." / "share" / "games" / "flourish"
end

DATA_DIR = determine_data_dir()

def load_image(name)
  return yogame.load_image(DATA_DIR / name)
end

def open_font(pt)
  path = DATA_DIR / "OpenBaskerville-0.0.53.otf"
  if (font = sdl.ttf.TTF_OpenFont(path, pt)) == nil
    raise yogame.SdlError.new("TTF_OpenFont failed")
  end
  return font
end

def close_font(font)
  sdl.ttf.TTF_CloseFont(font)
end

def make_color(r, g, b)
  color = sdl.SDL_Color.new()
  color.r = r
  color.g = g
  color.b = b
  return color
end

def play_sound(chunk)
  if sdl.mixer.Mix_PlayChannel(-1, chunk, 0) == -1
    raise yogame.SdlError.new("Mix_PlayChannel failed")
  end
end

CELL_SIZE = 80
CHAR_POS_OFFSET_X = CELL_SIZE // 2
CHAR_POS_OFFSET_Y = CHAR_POS_OFFSET_X

MAIN_VIEW_X = 120
MAIN_VIEW_Y = 40
MAIN_VIEW_WIDTH = 400
MAIN_VIEW_HEIGHT = 400
MAIN_VIEW_PC_X = MAIN_VIEW_WIDTH // 2
MAIN_VIEW_PC_Y = MAIN_VIEW_HEIGHT - CELL_SIZE

FPS = 24

class Rect
  def init(x, y, width=CELL_SIZE, height=CELL_SIZE)
    self.x = x
    self.y = y
    self.width = width
    self.height = height
  end

  def include?(x, y)
    if (x < self.x) || (self.x + self.width < x)
      return false
    end
    if (y < self.y) || (self.y + self.height < y)
      return false
    end
    return true
  end
end

@include(Comparable)
class Point
  def init(x=nil, y=nil)
    self.x = x
    self.y = y
  end

  def <=>(point)
    if (diff = (point.x - self.x)) != 0
      return diff
    end
    return point.y - self.y
  end

  def +(point)
    return Point.new(self.x + point.x, self.y + point.y)
  end

  def *(n)
    return Point.new(n * self.x, n * self.y)
  end

  def to_s()
    return "<Point x={0}, y={1}>".format(self.x, self.y)
  end
end

class Resources
  def init(dir, names)
    self.dir = dir
    self.names = names
    self.map = nil
  end

  def allocate_resource(path)
    raise NotImplementedError.new()
  end

  def free_resource(rsc)
    raise NotImplementedError.new()
  end

  def allocate()
    self.map = {}
    self.names.each() do [name]
      self.map[name] = self.allocate_resource(self.dir / name)
    end
  end

  def free()
    self.map.each() do [key, value]
      self.free_resource(value)
    end
    self.map = nil
  end

  def [](name)
    return self.map[name]
  end
end

class Images > Resources
  def allocate_resource(path)
    return load_image(path)
  end

  def free_resource(rsc)
    sdl.SDL_FreeSurface(rsc)
  end
end

class Sounds > Resources
  def allocate_resource(path)
    if (chunk = sdl.mixer.Mix_LoadWAV(DATA_DIR / path)) == nil
      msg = "Mix_LoadWAV for {0} failed".format(DATA_DIR / path)
      raise yogame.SdlError.new(msg)
    end
    return chunk
  end

  def free_resource(rsc)
    sdl.mixer.Mix_FreeChunk(rsc)
  end
end

def dump_point(filename, lineno, point)
  puts("{0}:{1} x={2}, y={3}".format(filename, lineno, point.x, point.y))
end

def dump_rect(filename, lineno, rect)
  fmt = "{0}:{1} x={2}, y={3}, w={4}, h={5}"
  puts(fmt.format(filename, lineno, rect.x, rect.y, rect.w, rect.h))
end

# vim: tabstop=2 shiftwidth=2 expandtab softtabstop=2
